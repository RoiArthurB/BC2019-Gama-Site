<!DOCTYPE html><html lang="en"><head><meta charSet="utf-8"/><meta http-equiv="X-UA-Compatible" content="IE=edge"/><title>Comodel of Flood and Evacuation model · GAMA-Platform</title><meta name="viewport" content="width=device-width"/><meta name="generator" content="Docusaurus"/><meta name="description" content="GAMA"/><meta name="docsearch:language" content="en"/><meta property="og:title" content="Comodel of Flood and Evacuation model · GAMA-Platform"/><meta property="og:type" content="website"/><meta property="og:url" content="https://roiarthurb.github.io/BC2019-Gama-Site/"/><meta property="og:description" content="GAMA"/><meta property="og:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><meta name="twitter:card" content="summary"/><meta name="twitter:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><link rel="shortcut icon" href="/BC2019-Gama-Site//img/gama-logo.png"/><link rel="stylesheet" href="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.css"/><link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/default.min.css"/><link rel="stylesheet" href="/BC2019-Gama-Site//css/code-block-buttons.css"/><link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.8.2/css/all.css"/><script type="text/javascript" src="https://buttons.github.io/buttons.js" async=""></script><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/clipboard.js/2.0.0/clipboard.min.js" async=""></script><script type="text/javascript" src="/BC2019-Gama-Site/js/code-block-buttons.js" async=""></script><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/fuse.js/3.4.4/fuse.min.js" async=""></script><script src="https://unpkg.com/vanilla-back-to-top@7.1.14/dist/vanilla-back-to-top.min.js"></script><script>
        document.addEventListener('DOMContentLoaded', function() {
          addBackToTop(
            {"zIndex":100}
          )
        });
        </script><link rel="stylesheet" href="/BC2019-Gama-Site/css/main.css"/><script src="/BC2019-Gama-Site/js/codetabs.js"></script></head><body class="sideNavVisible separateOnPageNav"><div class="fixedHeaderContainer"><div class="headerWrapper wrapper"><header><a href="/BC2019-Gama-Site/"><img class="logo" src="/BC2019-Gama-Site//img/gama-logo_white.png" alt="GAMA-Platform"/><h2 class="headerTitleWithLogo">GAMA-Platform</h2></a><div class="navigationWrapper navigationSlider"><nav class="slidingNav"><ul class="nav-site nav-site-internal"><li class=""><a href="/BC2019-Gama-Site/wiki/Home" target="_self">Documentation</a></li><li class=""><a href="/BC2019-Gama-Site/wiki/Tutorials" target="_self">Tutorials</a></li><li class=""><a href="/BC2019-Gama-Site/help" target="_self">Download</a></li><li class=""><a href="/BC2019-Gama-Site/wiki/Contribute" target="_self">Contribute</a></li><li class=""><a target="_self"></a></li><li class="navSearchWrapper reactNavSearchWrapper"><input type="text" id="search_input_react" placeholder="Search" title="Search"/></li></ul></nav></div></header></div></div><div class="navPusher"><div class="docMainWrapper wrapper"><div class="container mainContainer"><div class="wrapper"><div class="post"><header class="postHeader"><h1 class="postHeaderTitle">Comodel of Flood and Evacuation model</h1></header><article><div><span><p><em>Author : HUYNH Quang Nghi</em></p>
<p>Co-model example : couple the evacuation model with the flood model. Water win or human win?</p>
<p>Imported models :</p>
<pre><code class="hljs">model continuous_move 
global { 
    <span class="hljs-comment">//Shapefile of the buildings</span>
    file building_shapefile &lt;- file(<span class="hljs-string">"../includes/building.shp"</span>);
    <span class="hljs-comment">//Shape of the environment</span>
    geometry shape &lt;- envelope(building_shapefile);
    int maximal_turn &lt;- 90; <span class="hljs-comment">//in degree</span>
    int cohesion_factor &lt;- 10;
    <span class="hljs-comment">//Size of the people</span>
    float people_size &lt;- 2.0;
    <span class="hljs-comment">//Space without buildings</span>
    geometry free_space;
    <span class="hljs-comment">//Number of people agent</span>
    int nb_people &lt;- 500;
    <span class="hljs-comment">//Point to evacuate</span>
    point target_point &lt;- {world.location.x, 0};
    init { 
        
        free_space &lt;- copy(shape);
        <span class="hljs-comment">//Creation of the buildinds</span>
        create building from: building_shapefile {
            <span class="hljs-comment">//Creation of the free space by removing the shape of the different buildings existing</span>
            free_space &lt;- free_space - (shape + people_size);
        }
        <span class="hljs-comment">//Simplification of the free_space to remove sharp edges</span>
        free_space &lt;- free_space simplification(1.0);
        <span class="hljs-comment">//Creation of the people agents</span>
        create people number: nb_people {
            <span class="hljs-comment">//People agents are placed randomly among the free space</span>
            location &lt;- any_location_in(free_space);
            target_loc &lt;-  target_point;
        }           
    }   
}
<span class="hljs-comment">//Species which represent the building </span>
species building {
    <span class="hljs-comment">//Height of the buildings</span>
    float height &lt;- 3.0 + rnd(5);
    aspect default {
        draw shape color: #gray depth: height;
    }
}
<span class="hljs-comment">//Species people which move to the evacuation point using the skill moving</span>
species people skills:[moving]{
    <span class="hljs-comment">//Target point to evacuate</span>
    point target_loc;
    <span class="hljs-comment">//Speed of the agent</span>
    float speed &lt;- 0.5 + rnd(1000) / 1000;
    <span class="hljs-comment">//Velocity of the agent</span>
    point velocity &lt;- {0,0};
    <span class="hljs-comment">//Direction of the agent taking in consideration the maximal turn an agent is able to make</span>
    int heading max: heading + maximal_turn min: heading - maximal_turn;
    
    <span class="hljs-comment">//Size of the agent</span>
    float size &lt;- people_size;
    rgb color &lt;- rgb(rnd(255),rnd(255),rnd(255));
        
    <span class="hljs-comment">//Reflex to kill the agent when it has evacuated the area</span>
    reflex end when: location distance_to target_loc &lt;= 2 * people_size{
        <span class="hljs-statement">write</span> name + <span class="hljs-string">" is arrived"</span>;
        do die;
    }
    <span class="hljs-comment">//Reflex to compute the velocity of the agent considering the cohesion factor</span>
    reflex follow_goal  {
        velocity &lt;- velocity + ((target_loc - location) / cohesion_factor);
    }
    <span class="hljs-comment">//Reflex to apply separation when people are too close from each other</span>
    reflex separation {
        point acc &lt;- {0,0};
        ask (people at_distance size)  {
            acc &lt;- acc - (location - myself.location);
        }  
        velocity &lt;- velocity + acc;
    }
    <span class="hljs-comment">//Reflex to avoid the different obstacles</span>
    reflex avoid { 
        point acc &lt;- {0,0};
        list&lt;building&gt; nearby_obstacles &lt;- (building at_distance people_size);
        loop obs over: nearby_obstacles {
            acc &lt;- acc - (obs.location - location);
        }
        velocity &lt;- velocity + acc; 
    }
    <span class="hljs-comment">//Reflex to move the agent considering its location, target and velocity</span>
    reflex move {
        point old_location &lt;- copy(location);
        do goto target: location + velocity ;
        if (not empty(building overlapping self )) {
            location &lt;- point((location closest_points_with free_space)[1]);
        }
        velocity &lt;- location - old_location;
    }   
    aspect default {
        draw pyramid(size) color: color;
        draw sphere(size/3) at: {location.x,location.y,size} color: color;
    }
}

experiment main type: gui {
    parameter <span class="hljs-string">"nb people"</span> var: nb_people min: 1 max: 1000;
    output {
        display map type: opengl {
            species building refresh: false;
            species people;
            graphics <span class="hljs-string">"exit"</span> refresh: false {
                draw sphere(2 * people_size) at: target_point color: #green;    
            }
        }
    }
}

</code></pre>
<pre><code class="hljs">model Evacuation_coupling

import <span class="hljs-string">"../../../Toy Models/Evacuation/models/Continuous Move.gaml"</span>
experiment Evacuation_coupling_exp type: gui parent: main
{
    point centroid &lt;- { 0, 180 };
    list&lt;building&gt; getBuilding
    {
        return list(building);
    }

    action transform_environement
    {
        loop t over: list(building)
        {
            t.shape &lt;- t.shape translated_by centroid;
            t.shape &lt;- t.shape * 10;
            t.location &lt;- t.location * 8;
        }

        loop t over: list(people)
        {
            t.speed &lt;- 10.0;
            t.size &lt;- 20.0;
            t.shape &lt;- t.shape translated_by centroid;
            t.location &lt;- t.location * 8;
        }

        target_point &lt;- point(target_point translated_by centroid);
    }

    list&lt;people&gt; getPeople
    {
        return list(people);
    }

    output
    {
    }

}
</code></pre>
<pre><code class="hljs">
model hydro

global {
   <span class="hljs-comment">//Shapefile for the river</span>
   file river_shapefile &lt;- file(<span class="hljs-string">"../includes/RedRiver.shp"</span>);
   <span class="hljs-comment">//Shapefile for the dykes</span>
   file dykes_shapefile &lt;- file(<span class="hljs-string">"../includes/Dykes.shp"</span>);
   <span class="hljs-comment">//Shapefile for the buildings</span>
   file buildings_shapefile &lt;- file(<span class="hljs-string">"../includes/Building.shp"</span>);
   
   <span class="hljs-comment">//Data elevation file</span>
   file dem_file &lt;- file(<span class="hljs-string">"../includes/mnt50.asc"</span>);  
   <span class="hljs-comment">//Diffusion rate</span>
   float diffusion_rate &lt;- 0.6;
   <span class="hljs-comment">//Height of the dykes</span>
   float dyke_height &lt;- 15.0;
   <span class="hljs-comment">//Width of the dyke</span>
   float dyke_width &lt;- 15.0;
    
   <span class="hljs-comment">//Shape of the environment using the dem file</span>
   geometry shape &lt;- envelope(dem_file);
   
   <span class="hljs-comment">//List of the drain and river cells</span>
   list&lt;cell&gt; drain_cells;
   list&lt;cell&gt; river_cells;
   
   
  
   float step &lt;- 1°h;
   
   init {
     <span class="hljs-comment">//Initialization of the cells</span>
      do init_cells;
     <span class="hljs-comment">//Initialization of the water cells</span>
      do init_water;
     <span class="hljs-comment">//Initialization of the river cells</span>
     river_cells &lt;- cell where (each.is_river);
     <span class="hljs-comment">//Initialization of the drain cells</span>
      drain_cells &lt;- cell where (each.is_drain);
     <span class="hljs-comment">//Initialization of the obstacles (buildings and dykes)</span>
      do init_obstacles;
      <span class="hljs-comment">//Set the height of each cell</span>
      ask cell {
         obstacle_height &lt;- compute_highest_obstacle();
         do update_color;
      }
   }
   <span class="hljs-comment">//Action to initialize the altitude value of the cell according to the dem file</span>
   action init_cells {
      ask cell {
         altitude &lt;- grid_value;
         neighbor_cells &lt;- (self neighbors_at 1) ;
      }
   }
   <span class="hljs-comment">//action to initialize the water cells according to the river shape file and the drain</span>
   action init_water {
      geometry river &lt;- geometry(river_shapefile);
      ask cell overlapping river {
         water_height &lt;- 10.0;
         is_river &lt;- true;
         is_drain &lt;- grid_y = matrix(cell).rows - 1;
      }
   }
   <span class="hljs-comment">//initialization of the obstacles (the buildings and the dykes)</span>
   action init_obstacles{
      create buildings from: buildings_shapefile  {
         do update_cells;
      }
      create dyke from: dykes_shapefile;
      ask dyke {
          shape &lt;-  shape + dyke_width;
            do update_cells;
      }
   }
   <span class="hljs-comment">//Reflex to add water among the water cells</span>
   reflex adding_input_water {
      float water_input &lt;- rnd(100)/100;
      ask river_cells {
         water_height &lt;- water_height + water_input;
      }
   }
   <span class="hljs-comment">//Reflex to flow the water according to the altitute and the obstacle</span>
   reflex flowing {
      ask cell {already &lt;- false;}
      ask (cell sort_by ((each.altitude + each.water_height + each.obstacle_height))) {
         do flow;
      }
   }
   <span class="hljs-comment">//Reflex to update the color of the cell</span>
   reflex update_cell_color {
      ask cell {
         do update_color;
      }
   }
   <span class="hljs-comment">//Reflex for the drain cells to drain water</span>
   reflex draining {
      ask drain_cells {
         water_height &lt;- 0.0;
      }
   }
   
}
<span class="hljs-comment">//Species which represent the obstacle</span>
   species obstacle {
      <span class="hljs-comment">//height of the obstacle</span>
      float height min: 0.0;
      <span class="hljs-comment">//Color of the obstacle</span>
      rgb color;
      <span class="hljs-comment">//Pressure of the water</span>
      float water_pressure update: compute_water_pressure();
      
      <span class="hljs-comment">//List of cells concerned</span>
      list&lt;cell&gt; cells_concerned ;
      <span class="hljs-comment">//List of cells in the neighborhood </span>
      list&lt;cell&gt; cells_neighbors;
      
      <span class="hljs-comment">//Action to compute the water pressure</span>
      float compute_water_pressure {
        <span class="hljs-comment">//If the obstacle doesn't have height, then there will be no pressure</span>
         if (height = 0.0) {
            return 0.0;
         } else {
            <span class="hljs-comment">//The leve of the water is equals to the maximul level of water in the neighbors cells</span>
            float water_level &lt;- cells_neighbors max_of (each.water_height);
            <span class="hljs-comment">//Return the water pressure as the minimal value between 1 and the water level divided by the height</span>
            return min([1.0,water_level / height]);
         } 
      }
      
      <span class="hljs-comment">//Action to update the cells</span>
      action update_cells {
        <span class="hljs-comment">//All the cells concerned by the obstacle are the ones overlapping the obstacle</span>
         cells_concerned &lt;- (cell overlapping self);
            ask cells_concerned {
            <span class="hljs-comment">//Add the obstacles to the obstacles of the cell</span>
            add myself to: obstacles;
            water_height &lt;- 0.0;
         }
         <span class="hljs-comment">//Cells neighbors are all the neighbors cells of the cells concerned</span>
         cells_neighbors &lt;- cells_concerned + cells_concerned accumulate (each.neighbor_cells);
         <span class="hljs-comment">//The height is now computed</span>
         do compute_height();
         if (height &gt; 0.0) {   
            <span class="hljs-comment">//We compute the water pressure again</span>
            water_pressure &lt;- compute_water_pressure();
         } else {water_pressure &lt;- 0.0;}
      }
      action compute_height;
      aspect geometry {
         int val &lt;- int( 255 * water_pressure);
         color &lt;- rgb(val,255-val,0);
         draw shape color: color depth: height*5 border: color;
      }
   }
   <span class="hljs-comment">//Species buildings which is derivated from obstacle</span>
   species buildings parent: obstacle {
     <span class="hljs-comment">//The building has a height randomly chosed between 2 and 10</span>
      float height &lt;- 2.0 + rnd(8);
   }
   <span class="hljs-comment">//Species dyke which is derivated from obstacle</span>
   species dyke parent: obstacle{
    
       int counter_wp &lt;- 0;
       int breaking_threshold &lt;- 24;
      
      <span class="hljs-comment">//Action to represent the break of the dyke</span>
       action break{
         ask cells_concerned {
            do update_after_destruction(myself);
         }
         do die;
      }
      <span class="hljs-comment">//Action to compute the height of the dyke as the dyke_height without the mean height of the cells it overlaps</span>
      action compute_height
       {
           height &lt;- dyke_height - mean(cells_concerned collect (each.altitude));
      
      }
      
      <span class="hljs-comment">//Reflex to break the dynamic of the water</span>
      reflex breaking_dynamic {
        if (water_pressure = 1.0) {
            counter_wp &lt;- counter_wp + 1;
            if (counter_wp &gt; breaking_threshold) {
                do break;
            }
        } else {
            counter_wp &lt;- 0;
        }
      }
      <span class="hljs-comment">//user command which allows the possibility to destroy the dyke for the user</span>
      user_command <span class="hljs-string">"Destroy dyke"</span> action: break; 
   }
   <span class="hljs-comment">//Grid cell to discretize space, initialized using the dem file</span>
   grid cell file: dem_file neighbors: 8 frequency: 0  use_regular_agents: false use_individual_shapes: false use_neighbors_cache: false {
      <span class="hljs-comment">//Altitude of the cell</span>
      float altitude;
      <span class="hljs-comment">//Height of the water in the cell</span>
      float water_height &lt;- 0.0 min: 0.0;
      <span class="hljs-comment">//Height of the cell</span>
      float height;
      <span class="hljs-comment">//List of the neighbor cells</span>
      list&lt;cell&gt; neighbor_cells ;
      <span class="hljs-comment">//Boolean to know if it is a drain cell</span>
      bool is_drain &lt;- false;
      <span class="hljs-comment">//Boolean to know if it is a river cell</span>
      bool is_river &lt;- false;
      <span class="hljs-comment">//List of all the obstacles overlapping the cell</span>
      list&lt;obstacle&gt; obstacles;
      <span class="hljs-comment">//Height of the obstacles</span>
      float obstacle_height &lt;- 0.0;
      bool already &lt;- false;
      
      <span class="hljs-comment">//Action to compute the highest obstacle among the obstacles</span>
      float compute_highest_obstacle {
         if (empty(obstacles))
         {
            return 0.0; 
         } else {
            return obstacles max_of(each.height);
         }
      }
      <span class="hljs-comment">//Action to flow the water </span>
      action flow {
        <span class="hljs-comment">//if the height of the water is higher than 0 then, it can flow among the neighbor cells</span>
         if (water_height &gt; 0) {
            <span class="hljs-comment">//We get all the cells already done</span>
            list&lt;cell&gt; neighbor_cells_al &lt;- neighbor_cells where (each.already);
            <span class="hljs-comment">//If there are cells already done then we continue</span>
            if (!empty(neighbor_cells_al)) {
               <span class="hljs-comment">//We compute the height of the neighbors cells according to their altitude, water_height and obstacle_height</span>
               ask neighbor_cells_al {height &lt;- altitude + water_height + obstacle_height;}
               <span class="hljs-comment">//The height of the cell is equals to its altitude and water height</span>
               height &lt;-  altitude +  water_height;
               <span class="hljs-comment">//The water of the cells will flow to the neighbor cells which have a height less than the height of the actual cell</span>
               list&lt;cell&gt; flow_cells &lt;- (neighbor_cells_al where (height &gt; each.height)) ;
               <span class="hljs-comment">//If there are cells, we compute the water flowing</span>
               if (!empty(flow_cells)) {
                  loop flow_cell over: shuffle(flow_cells) sort_by (each.height){
                     float water_flowing &lt;- max([0.0, min([(height - flow_cell.height), water_height * diffusion_rate])]); 
                     water_height &lt;- water_height - water_flowing;
                     flow_cell.water_height &lt;-flow_cell.water_height +  water_flowing;
                     height &lt;- altitude + water_height;
                  }   
               }
            }
         }
         already &lt;- true;
      }  
      <span class="hljs-comment">//Update the color of the cell</span>
      action update_color { 
         int val_water &lt;- 0;
         val_water &lt;- max([0, min([255, int(255 * (1 - (water_height / 12.0)))])]) ;  
         color &lt;- rgb([val_water, val_water, 255]);
         grid_value &lt;- water_height + altitude;
      }
      <span class="hljs-comment">//action to compute the destruction of the obstacle</span>
      action update_after_destruction(obstacle the_obstacle){
         remove the_obstacle from: obstacles;
         obstacle_height &lt;- compute_highest_obstacle();
      }
       
   }


experiment main_gui type: gui {
   parameter <span class="hljs-string">"Shapefile for the river"</span> var:river_shapefile category:<span class="hljs-string">"Water data"</span>;
   parameter <span class="hljs-string">"Shapefile for the dykes"</span> var:dykes_shapefile category:<span class="hljs-string">"Obstacles"</span>;
   parameter <span class="hljs-string">"Shapefile for the buildings"</span> var:buildings_shapefile category:<span class="hljs-string">"Obstacles"</span>;
   parameter <span class="hljs-string">"Height of the dykes"</span> var:dyke_height category:<span class="hljs-string">"Obstacles"</span>;
   parameter <span class="hljs-string">"Diffusion rate"</span> var:diffusion_rate category:<span class="hljs-string">"Water dynamic"</span>;
   output { 
      display map type: opengl {
         grid cell triangulation: true;
         species buildings aspect: geometry;
         species dyke aspect: geometry ;
      }
      display chart_display refresh: every(24) { 
         chart <span class="hljs-string">"Pressure on Dykes"</span> type: series {
            data <span class="hljs-string">"Mean pressure on dykes "</span> value: mean(dyke collect (each.water_pressure)) style: line color: #magenta ;
            data <span class="hljs-string">"Rate of dykes with max pressure"</span> value: (dyke count (each.water_pressure = 1.0))/ length(dyke) style: line color: #red ;
            data <span class="hljs-string">"Rate of dykes with high pressure"</span> value: (dyke count (each.water_pressure &gt; 0.5))/ length(dyke) style: line color: #orange ;
            data <span class="hljs-string">"Rate of dykes with low pressure"</span> value: (dyke count (each.water_pressure &lt; 0.25))/ length(dyke) style: line color: #green ;
         }
      }
   }
}
</code></pre>
<pre><code class="hljs">model Flood_coupling

import <span class="hljs-string">"../../../Toy Models/Flood Simulation/models/Hydrological Model.gaml"</span>
experiment Flood_coupling_exp type: gui parent: main_gui
{
    point newSize &lt;- { 0.07, 0.07 };
    cell getCellAt (geometry p)
    {
        ask simulation
        {
            return cell closest_to p;
        }

    }

    list&lt;cell&gt; getCell
    {
        return list(cell) where (each.grid_value &gt; 8.0);
    }

    list&lt;buildings&gt; getBuildings
    {
        return list(buildings);
    }

    list&lt;dyke&gt; getDyke
    {
        return list(dyke);
    }

    output
    {
    }

}
</code></pre>
<p>Code of the model :</p>
<pre><code class="hljs">model comodel_Flood_Evacuation

import <span class="hljs-string">"Flood_coupling.gaml"</span> as myFlood
import <span class="hljs-string">"Evacuation_coupling.gaml"</span> as myEvacuation


global
{
    <span class="hljs-comment">//set the bound of environment</span>
    geometry shape &lt;- envelope(file(<span class="hljs-string">"../../../Toy Models/Flood Simulation/includes/mnt50.asc"</span>));
    <span class="hljs-comment">//counting variable of casualty</span>
    int casualty &lt;- 0;
    init
    {
        <span class="hljs-comment">//create experiment from micro-model myFlood with corresponding parameters</span>
        create myFlood.Flood_coupling_exp with:
        [buildings_shapefile::file(<span class="hljs-string">"../../../Toy Models/Flood Simulation/includes/Building.shp"</span>), river_shapefile::file(<span class="hljs-string">"../../../Toy Models/Flood Simulation/includes/RedRiver.shp"</span>), dykes_shapefile::file(<span class="hljs-string">"../../../Toy Models/Flood Simulation/includes/Dykes.shp"</span>), dem_file::file(<span class="hljs-string">"../../../Toy Models/Flood Simulation/includes/mnt50.asc"</span>)];
        <span class="hljs-comment">//create the Evacuation micro-model's experiment</span>
        create myEvacuation.Evacuation_coupling_exp with: [nb_people::200, target_point::{ 0, 1580 }, building_shapefile::file(<span class="hljs-string">"../../../Toy Models/Evacuation/includes/building.shp"</span>)]
        {
            <span class="hljs-comment">//transform the environment and the agents to new location (near the river)</span>
            do transform_environement;
        }

    }

    reflex doing_cosimulation
    {
        <span class="hljs-comment">//do a step of Flooding</span>
        ask myFlood.Flood_coupling_exp collect each.simulation
        {
            do _step_;
        }

        <span class="hljs-comment">//people evacate </span>
        ask myEvacuation.Evacuation_coupling_exp collect each.simulation
        {
            <span class="hljs-comment">//depending on the real plan of evacuation, we can test the speed of the evacuation with the speed of flooding by doing more or less simulation step  </span>
            if (cycle mod 2 = 0)
            {
                do _step_;
            }

        }

        <span class="hljs-comment">//loop over the population</span>
        loop thePeople over: first(myEvacuation.Evacuation_coupling_exp).getPeople()
        {
            <span class="hljs-comment">//get the cell at people's location</span>
            cell theWater &lt;- cell(first(myFlood.Flood_coupling_exp).getCellAt(thePeople));
            <span class="hljs-comment">//if the water levele is high than 8 meters and people is overlapped, tell him that he must dead</span>
            if (theWater.grid_value &gt; 8.0 and theWater overlaps thePeople)
            {
                ask thePeople
                {
                    do die;
                }
                <span class="hljs-comment">//increase the counting variable</span>
                casualty &lt;- casualty + 1;
            }

        }

    }

}

experiment comodel_Flood_Evacuation_exp type: gui
{
    output
    {
        display <span class="hljs-string">"comodel_disp"</span>
        {
            agents <span class="hljs-string">"building"</span> value: first(myEvacuation.Evacuation_coupling_exp).getBuilding();
            agents <span class="hljs-string">"people"</span> value: first(myEvacuation.Evacuation_coupling_exp).getPeople();
            agents <span class="hljs-string">"cell"</span> value: first(myFlood.Flood_coupling_exp).getCell();
            agents <span class="hljs-string">"dyke"</span> value: first(myFlood.Flood_coupling_exp).getDyke();
            graphics <span class="hljs-string">'CasualtyView'</span>
            {
                draw (<span class="hljs-string">'Casualty: '</span> + casualty) at: { 0, 4000 } font: font(<span class="hljs-string">"Arial"</span>, 18, # bold) color: # red;
            }

        }

    }

}
</code></pre>
</span></div></article></div><div class="docs-prevnext"></div></div></div><nav class="onPageNav"></nav></div><footer class="nav-footer" id="footer"><section class="sitemap"><a href="/BC2019-Gama-Site/" class="nav-home"><img src="/BC2019-Gama-Site//img/gama-logo.png" alt="GAMA-Platform" width="66" height="58"/></a><div><h5>Networks</h5><a href="https://www.youtube.com/channel/UCWJ1kWGDDI-9u2f2uD0gcaQ" target="_blank"><i class="fab fa-youtube"></i> Youtube</a><a href="https://www.linkedin.com/company/gama-platform" target="_blank"><i class="fab fa-linkedin"></i> LinkedIn</a><a href="https://twitter.com/gamaplatform" target="_blank"><i class="fab fa-twitter"></i> Twitter</a><a href="https://www.facebook.com/GamaPlatform/" target="_blank"><i class="fab fa-facebook"></i> Facebook</a></div><div><h5>Mailing list</h5><a href="https://groups.google.com/forum/#!forum/gama-platform" target="_blank"><i class="fas fa-envelope"></i> For Users<br/>gama-platform@googlegroups.com</a><a href="https://groups.google.com/forum/#!forum/gama-dev" target="_blank"><i class="fas fa-envelope"></i> For Developers<br/>gama-dev@googlegroups.com</a></div><div><h5>Ecosystem</h5><a href="https://github.com/gama-platform/gama"><i class="fab fa-github"></i> GitHub Gama</a><a href="https://github.com/gama-platform/gama.experimental"><i class="fas fa-puzzle-piece"></i> Gama Experimental Plugins</a><a href="https://github.com/gama-platform/gama.models.experimental"><i class="fas fa-scroll"></i> Gama Experimental Models</a></div></section><section class="copyright">Copyright © 2019 GAMA-Platform</section></footer></div><script type="text/javascript" src="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.js"></script><script>
                document.addEventListener('keyup', function(e) {
                  if (e.target !== document.body) {
                    return;
                  }
                  // keyCode for '/' (slash)
                  if (e.keyCode === 191) {
                    const search = document.getElementById('search_input_react');
                    search && search.focus();
                  }
                });
              </script><script>
              var search = docsearch({
                
                apiKey: '0f9f28b9ab9efae89810921a351753b5',
                indexName: 'github',
                inputSelector: '#search_input_react'
              });
            </script></body></html>