<!DOCTYPE html><html lang="en"><head><meta charSet="utf-8"/><meta http-equiv="X-UA-Compatible" content="IE=edge"/><title>Complex Road Network · GAMA-Platform</title><meta name="viewport" content="width=device-width"/><meta name="generator" content="Docusaurus"/><meta name="description" content="GAMA"/><meta name="docsearch:language" content="en"/><meta property="og:title" content="Complex Road Network · GAMA-Platform"/><meta property="og:type" content="website"/><meta property="og:url" content="https://roiarthurb.github.io/BC2019-Gama-Site/"/><meta property="og:description" content="GAMA"/><meta property="og:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><meta name="twitter:card" content="summary"/><meta name="twitter:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><link rel="shortcut icon" href="/BC2019-Gama-Site//img/gama-logo.png"/><link rel="stylesheet" href="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.css"/><link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/default.min.css"/><link rel="stylesheet" href="/BC2019-Gama-Site/css/code-block-buttons.css"/><link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.8.2/css/all.css"/><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/fuse.js/3.4.4/fuse.min.js"></script><script type="text/javascript" src="https://buttons.github.io/buttons.js"></script><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/clipboard.js/2.0.0/clipboard.min.js"></script><script type="text/javascript" src="/BC2019-Gama-Site/js/code-block-buttons.js"></script><script type="text/javascript" src="/BC2019-Gama-Site/js/searchEngine.js"></script><script src="https://unpkg.com/vanilla-back-to-top@7.1.14/dist/vanilla-back-to-top.min.js"></script><script>
        document.addEventListener('DOMContentLoaded', function() {
          addBackToTop(
            {"zIndex":100}
          )
        });
        </script><link rel="stylesheet" href="/BC2019-Gama-Site/css/main.css"/><script src="/BC2019-Gama-Site/js/codetabs.js"></script></head><body class="sideNavVisible separateOnPageNav"><div class="fixedHeaderContainer"><div class="headerWrapper wrapper"><header><a href="/BC2019-Gama-Site/"><img class="logo" src="/BC2019-Gama-Site//img/gama-logo_white.png" alt="GAMA-Platform"/><h2 class="headerTitleWithLogo">GAMA-Platform</h2></a><div class="navigationWrapper navigationSlider"><nav class="slidingNav"><ul class="nav-site nav-site-internal"><li class=""><a href="/BC2019-Gama-Site/wiki/Home" target="_self">Documentation</a></li><li class=""><a href="/BC2019-Gama-Site/wiki/Tutorials" target="_self">Tutorials</a></li><li class=""><a href="/BC2019-Gama-Site/help" target="_self">Download</a></li><li class=""><a href="/BC2019-Gama-Site/wiki/Contribute" target="_self">Contribute</a></li><li class=""><a target="_self"></a></li><li class="navSearchWrapper reactNavSearchWrapper"><input type="text" id="search_input_react" placeholder="Search" title="Search"/></li></ul></nav></div></header></div></div><div class="navPusher"><div class="docMainWrapper wrapper"><div class="container mainContainer"><div class="wrapper"><div class="post"><header class="postHeader"><h1 class="postHeaderTitle">Complex Road Network</h1></header><article><div><span><p><em>Author : Patrick Taillandier</em></p>
<p>Model to show how to use the driving skill to represent the traffic on a road network generated thanks to shapefiles, with intersections and traffic lights going from red to green to let people move or stop. Two experiments are presented : experiment_2D to display the model in 2D and which better display the orientation of roads and experiment_3D to display the model in 3D.</p>
<p>Code of the model :</p>
<pre><code class="hljs">
model RoadTrafficComplex
 
global {   
    bool simple_data &lt;- false;
    
    <span class="hljs-comment">//Check if we use simple data or more complex roads</span>
    file shape_file_roads  &lt;- simple_data ? file(<span class="hljs-string">"../includes/RoadCircleLanes.shp"</span>): file(<span class="hljs-string">"../includes/ManhattanRoads.shp"</span>) ;
    file shape_file_nodes  &lt;- simple_data ? file(<span class="hljs-string">"../includes/NodeCircleLanes.shp"</span>) : file(<span class="hljs-string">"../includes/ManhattanNodes.shp"</span>);
    file shape_file_bounds &lt;- simple_data ? file(<span class="hljs-string">"../includes/BoundsLaneRoad.shp"</span>) :file(<span class="hljs-string">"../includes/ManhattanRoads.shp"</span>);
    geometry shape &lt;- envelope(shape_file_bounds) + 50.0;
    
    graph road_network;  
    int nb_people &lt;- simple_data ? 20 : 500;
     
    init {  
        <span class="hljs-comment">//create the intersection and check if there are traffic lights or not by looking the values inside the type column of the shapefile and linking</span>
        <span class="hljs-comment">// this column to the attribute is_traffic_signal. </span>
        create intersection from: shape_file_nodes with:[is_traffic_signal::(read(<span class="hljs-string">"type"</span>) = <span class="hljs-string">"traffic_signals"</span>)];
        
        <span class="hljs-comment">//create road agents using the shapefile and using the oneway column to check the orientation of the roads if there are directed</span>
        create road from: shape_file_roads with:[lanes::int(read(<span class="hljs-string">"lanes"</span>)), oneway::string(read(<span class="hljs-string">"oneway"</span>))] {
            geom_display &lt;- shape + (2.5 * lanes);
            maxspeed &lt;- (lanes = 1 ? 30.0 : (lanes = 2 ? 50.0 : 70.0)) °km/°h;
            switch oneway {
                match <span class="hljs-string">"no"</span> {
                    create road {
                        lanes &lt;- max([1, int (myself.lanes / 2.0)]);
                        shape &lt;- polyline(reverse(myself.shape.points));
                        maxspeed &lt;- myself.maxspeed;
                        geom_display  &lt;- myself.geom_display;
                        linked_road &lt;- myself;
                        myself.linked_road &lt;- self;
                    }
                    lanes &lt;- int(lanes /2.0 + 0.5);
                }
                match <span class="hljs-string">"-1"</span> {
                    shape &lt;- polyline(reverse(shape.points));
                }   
            }
        }   
        map general_speed_map &lt;- road as_map (each::(each.shape.perimeter / each.maxspeed));
        
        <span class="hljs-comment">//creation of the road network using the road and intersection agents</span>
        road_network &lt;-  (as_driving_graph(road, intersection))  with_weights general_speed_map;
        
        <span class="hljs-comment">//initialize the traffic light</span>
        ask intersection {
            do initialize;
        }
        
        create people number: nb_people { 
            max_speed &lt;- 160 °km/°h;
            vehicle_length &lt;- 5.0 °m;
            right_side_driving &lt;- true;
            proba_lane_change_up &lt;- 0.1 + (rnd(500) / 500);
            proba_lane_change_down &lt;- 0.5+ (rnd(500) / 500);
            location &lt;- one_of(intersection where empty(each.stop)).location;
            security_distance_coeff &lt;- 5/9 * 3.6 * (1.5 - rnd(1000) / 1000);  
            proba_respect_priorities &lt;- 1.0 - rnd(200/1000);
            proba_respect_stops &lt;- [1.0];
            proba_block_node &lt;- 0.0;
            proba_use_linked_road &lt;- 0.0;
            max_acceleration &lt;- 5/3.6;
            speed_coeff &lt;- 1.2 - (rnd(400) / 1000);
            threshold_stucked &lt;-int ( (1 + rnd(5))°mn);
            proba_breakdown &lt;- 0.00001;
            
        }   
    }
    
} 

<span class="hljs-comment">//species that will represent the intersection node, it can be traffic lights or not, using the skill_road_node skill</span>
species intersection skills: [skill_road_node] {
    bool is_traffic_signal;
    list&lt;list&gt; stop;
    int time_to_change &lt;- 100;
    int counter &lt;- rnd (time_to_change) ;
    list&lt;road&gt; ways1;
    list&lt;road&gt; ways2;
    bool is_green;
    rgb color_fire;
    
    action initialize {
        if (is_traffic_signal) {
            do compute_crossing;
            stop&lt;&lt; [];
            if (flip(0.5)) {
                do to_green;
            } else {
                do to_red;
            }   
        }
    }
    
    action compute_crossing{
        if  (length(roads_in) &gt;= 2) {
            road rd0 &lt;- road(roads_in[0]);
            list&lt;point&gt; pts &lt;- rd0.shape.points;                        
            float ref_angle &lt;-  float( last(pts) direction_to rd0.location);
            loop rd over: roads_in {
                list&lt;point&gt; pts2 &lt;- road(rd).shape.points;                      
                float angle_dest &lt;-  float( last(pts2) direction_to rd.location);
                float ang &lt;- abs(angle_dest - ref_angle);
                if (ang &gt; 45 and ang &lt; 135) or  (ang &gt; 225 and ang &lt; 315) {
                    ways2&lt;&lt; road(rd);
                }
            }
        }
        loop rd over: roads_in {
            if not(rd in ways2) {
                ways1 &lt;&lt; road(rd);
            }
        }
    }
    
    action to_green {
        stop[0] &lt;- ways2 ;
        color_fire &lt;- #green;
        is_green &lt;- true;
    }
    
    action to_red {
        stop[0] &lt;- ways1;
        color_fire &lt;- #red;
        is_green &lt;- false;
    }
    reflex dynamic_node when: is_traffic_signal  {
        counter &lt;- counter + 1;
        if (counter &gt;= time_to_change) { 
            counter &lt;- 0;
            if is_green {do to_red;}
            else {do to_green;}
        } 
    }
    
    aspect base {
        if (is_traffic_signal) {    
            draw circle(5) color: color_fire;
        }
    }
    
    aspect base3D {
        if (is_traffic_signal) {    
            draw box(1,1,10) color:#black;
            draw sphere(5) at: {location.x,location.y,12} color: color_fire;
        }
    }
}


<span class="hljs-comment">//species that will represent the roads, it can be directed or not and uses the skill skill_road</span>
species road skills: [skill_road] { 
    geometry geom_display;
    string oneway;
    aspect base {    
        draw shape color: #gray end_arrow: 10;
    } 
    aspect base3D {    
        draw geom_display color: #gray ;
    } 
}

<span class="hljs-comment">//People species that will move on the graph of roads to a target and using the skill advanced_driving</span>
species people skills: [advanced_driving] { 
    rgb color &lt;- rgb(rnd(255), rnd(255), rnd(255)) ;
    int counter_stucked &lt;- 0;
    int threshold_stucked;
    bool breakdown &lt;- false;
    float proba_breakdown ;
    intersection target;
    
    reflex breakdown when: flip(proba_breakdown){
        breakdown &lt;- true;
        max_speed &lt;- 1 °km/°h;
    }
    
    reflex time_to_go when: final_target = <span class="hljs-literal">nil</span> {
        target &lt;- one_of(intersection where not each.is_traffic_signal);
        current_path &lt;- compute_path(graph: road_network, target: target );
        if (current_path = <span class="hljs-literal">nil</span> ) {
            final_target &lt;- <span class="hljs-literal">nil</span>;
        }
    }
    reflex move when: current_path != <span class="hljs-literal">nil</span> and final_target != <span class="hljs-literal">nil</span> {
        do drive;
        if real_speed &lt; 5°km/°h {
            counter_stucked&lt;- counter_stucked + 1;
            if (counter_stucked mod threshold_stucked = 0) {
                proba_use_linked_road &lt;- min([1.0,proba_use_linked_road + 0.1]);
            }
        } else {
            counter_stucked&lt;- 0;
            proba_use_linked_road &lt;- 0.0;
        }
    }

    aspect base { 
        draw breakdown ? square(15) : triangle(15) color: color rotate:heading + 90;
    } 
    aspect base3D {
        point loc &lt;- calcul_loc();
        draw box(vehicle_length, 1,1) at: loc rotate:  heading color: color;
        
        draw triangle(0.5) depth: 1.5 at: loc rotate:  heading + 90 color: color;
        
        if (breakdown) {
            draw circle(2) at: loc color: #red;
        }
    } 
    
    point calcul_loc {
        if (current_road = <span class="hljs-literal">nil</span>) {
            return location;
        } else {
            float val &lt;- (road(current_road).lanes - current_lane) + 0.5;
            val &lt;- on_linked_road ? val * - 1 : val;
            if (val = 0) {
                return location; 
            } else {
                return (location + {cos(heading + 90) * val, sin(heading + 90) * val});
            }
        }
    }
    
} 

experiment experiment_2D type: gui {
    parameter <span class="hljs-string">"if true, simple data (simple track), if false complex one (Manhattan):"</span> var: simple_data category: <span class="hljs-string">"GIS"</span> ;
    output {
        display city_display {
            species road aspect: base ;
            species intersection aspect: base;
            species people aspect: base;
        }
    }
}

experiment experiment_3D type: gui {
    parameter <span class="hljs-string">"if true, simple data (simple track), if false complex one (Manhattan):"</span> var: simple_data category: <span class="hljs-string">"GIS"</span> ;
    output {
        display carte_principale type: opengl {
            species road aspect: base3D refresh: true;
            species intersection aspect: base3D;
            species people aspect: base3D ; 
        }
    }
}

</code></pre>
</span></div></article></div><div class="docs-prevnext"></div></div></div><nav class="onPageNav"></nav></div><footer class="nav-footer" id="footer"><section class="sitemap"><a href="/BC2019-Gama-Site/" class="nav-home"><img src="/BC2019-Gama-Site//img/gama-logo.png" alt="GAMA-Platform" width="66" height="58"/></a><div><h5>Networks</h5><a href="https://www.youtube.com/channel/UCWJ1kWGDDI-9u2f2uD0gcaQ" target="_blank"><i class="fab fa-youtube"></i> Youtube</a><a href="https://www.linkedin.com/company/gama-platform" target="_blank"><i class="fab fa-linkedin"></i> LinkedIn</a><a href="https://twitter.com/gamaplatform" target="_blank"><i class="fab fa-twitter"></i> Twitter</a><a href="https://www.facebook.com/GamaPlatform/" target="_blank"><i class="fab fa-facebook"></i> Facebook</a></div><div><h5>Mailing list</h5><a href="https://groups.google.com/forum/#!forum/gama-platform" target="_blank"><i class="fas fa-envelope"></i> For Users<br/>gama-platform@googlegroups.com</a><a href="https://groups.google.com/forum/#!forum/gama-dev" target="_blank"><i class="fas fa-envelope"></i> For Developers<br/>gama-dev@googlegroups.com</a></div><div><h5>Ecosystem</h5><a href="https://github.com/gama-platform/gama"><i class="fab fa-github"></i> GitHub Gama</a><a href="https://github.com/gama-platform/gama.experimental"><i class="fas fa-puzzle-piece"></i> Gama Experimental Plugins</a><a href="https://github.com/gama-platform/gama.models.experimental"><i class="fas fa-scroll"></i> Gama Experimental Models</a></div></section><section class="copyright">Copyright © 2019 GAMA-Platform</section></footer></div><script type="text/javascript" src="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.js"></script><script>
                document.addEventListener('keyup', function(e) {
                  if (e.target !== document.body) {
                    return;
                  }
                  // keyCode for '/' (slash)
                  if (e.keyCode === 191) {
                    const search = document.getElementById('search_input_react');
                    search && search.focus();
                  }
                });
              </script><script>
              var search = docsearch({
                
                apiKey: '0f9f28b9ab9efae89810921a351753b5',
                indexName: 'github',
                inputSelector: '#search_input_react'
              });
            </script></body></html>