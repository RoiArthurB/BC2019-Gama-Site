<!DOCTYPE html><html lang="en"><head><meta charSet="utf-8"/><meta http-equiv="X-UA-Compatible" content="IE=edge"/><title>Boids Boids With Flocks · GAMA-Platform</title><meta name="viewport" content="width=device-width"/><meta name="generator" content="Docusaurus"/><meta name="description" content="GAMA, modeling made easy"/><meta name="docsearch:language" content="en"/><meta property="og:title" content="Boids Boids With Flocks · GAMA-Platform"/><meta property="og:type" content="website"/><meta property="og:url" content="https://roiarthurb.github.io/BC2019-Gama-Site/"/><meta property="og:description" content="GAMA, modeling made easy"/><meta property="og:image" content="https://roiarthurb.github.io/BC2019-Gama-Site/resources/images/general/GamaPlatform.png"/><meta name="twitter:card" content="summary"/><meta name="twitter:image" content="https://roiarthurb.github.io/BC2019-Gama-Site/resources/images/general/GamaPlatform.png"/><link rel="shortcut icon" href="/BC2019-Gama-Site/resources/images/general/GamaPlatform.png"/><link rel="stylesheet" href="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.css"/><link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/default.min.css"/><link rel="alternate" type="application/atom+xml" href="https://roiarthurb.github.io/BC2019-Gama-Site/blog/atom.xml" title="GAMA-Platform Blog ATOM Feed"/><link rel="alternate" type="application/rss+xml" href="https://roiarthurb.github.io/BC2019-Gama-Site/blog/feed.xml" title="GAMA-Platform Blog RSS Feed"/><link rel="stylesheet" href="/css/code-block-buttons.css"/><script type="text/javascript" src="https://buttons.github.io/buttons.js" async=""></script><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/clipboard.js/2.0.0/clipboard.min.js" async=""></script><script type="text/javascript" src="/BC2019-Gama-Site/js/code-block-buttons.js" async=""></script><link rel="stylesheet" href="/BC2019-Gama-Site/css/main.css"/><script src="/BC2019-Gama-Site/js/codetabs.js"></script></head><body class="sideNavVisible"><div class="fixedHeaderContainer"><div class="headerWrapper wrapper"><header><a href="/BC2019-Gama-Site/"><img class="logo" src="/BC2019-Gama-Site/resources/images/general/GamaPlatform.png" alt="GAMA-Platform"/><h2 class="headerTitleWithLogo">GAMA-Platform</h2></a><div class="navigationWrapper navigationSlider"><nav class="slidingNav"><ul class="nav-site nav-site-internal"><li class=""><a href="/BC2019-Gama-Site/docs/Home" target="_self">Docs</a></li><li class=""><a href="/BC2019-Gama-Site/help" target="_self">Help</a></li><li class=""><a href="/BC2019-Gama-Site/blog/" target="_self">Blog</a></li><li class="navSearchWrapper reactNavSearchWrapper"><input type="text" id="search_input_react" placeholder="Search" title="Search"/></li></ul></nav></div></header></div></div><div class="navPusher"><div class="docMainWrapper wrapper"><div class="container mainContainer"><div class="wrapper"><div class="post"><header class="postHeader"><h1 class="postHeaderTitle">Boids Boids With Flocks</h1></header><article><div><span><h1><a class="anchor" aria-hidden="true" id="boids-with-flocks"></a><a href="#boids-with-flocks" aria-hidden="true" class="hash-link"><svg class="hash-link-icon" aria-hidden="true" height="16" version="1.1" viewBox="0 0 16 16" width="16"><path fill-rule="evenodd" d="M4 9h1v1H4c-1.5 0-3-1.69-3-3.5S2.55 3 4 3h4c1.45 0 3 1.69 3 3.5 0 1.41-.91 2.72-2 3.25V8.59c.58-.45 1-1.27 1-2.09C10 5.22 8.98 4 8 4H4c-.98 0-2 1.22-2 2.5S3 9 4 9zm9-3h-1v1h1c1 0 2 1.22 2 2.5S13.98 12 13 12H9c-.98 0-2-1.22-2-2.5 0-.83.42-1.64 1-2.09V6.25c-1.09.53-2 1.84-2 3.25C6 11.31 7.55 13 9 13h4c1.45 0 3-1.69 3-3.5S14.5 6 13 6z"></path></svg></a>Boids With Flocks</h1>
<p>_Author : _</p>
<p>This model shows the movement of boids following a goal, and creating without their own volonty, a flock .</p>
<p>Imported model :</p>
<pre><code class="hljs">
model boids 
global torus: torus_environment{ 
    //Number of boids that will be created
    int number_of_agents &lt;- 50 min: 1 max: 1000000;
    //Number of obstacles for the boids movement to represent
    int number_of_obstacles &lt;- 0 min: 0;
    //Maximal speed of the boids
    float maximal_speed &lt;- 15.0 min: 0.1 max: 15.0;
    //Factors for the group of boids
    int cohesion_factor &lt;- 200;
    int alignment_factor &lt;- 100; 
    //Variables for the movement of the boids
    float minimal_distance &lt;- 10.0; 
    int maximal_turn &lt;- 45 min: 0 max: 359; 
    
    int width_and_height_of_environment &lt;- 1000;  
    bool torus_environment &lt;- false; 
    bool apply_cohesion &lt;- true ;
    bool apply_alignment &lt;- true ;
    bool apply_separation &lt;- true;
    bool apply_avoid &lt;- true;  
    bool apply_wind &lt;- true;   
    bool moving_obstacles &lt;- false;   
    int bounds &lt;- int(width_and_height_of_environment / 20); 
    //Vector for the wind
    point wind_vector &lt;- {0,0}; 
    list images of: image_file &lt;- [file('../images/bird1.png'),file('../images/bird2.png'),file('../images/bird3.png')]; 
    int xmin &lt;- bounds;   
    int ymin &lt;- bounds;  
    int xmax &lt;- (width_and_height_of_environment - bounds);     
    int ymax &lt;- (width_and_height_of_environment - bounds);   
    
    //Action to move the goal to the mouse location
    action move_goal(point mouse) {
        ask first(boids_goal) {
            do goto target: mouse speed: 30;
        }
    }
    
    geometry shape &lt;- square(width_and_height_of_environment);
    
    init { 
        //Create the boids agents
        create boids number: number_of_agents { 
             location &lt;- {rnd (width_and_height_of_environment - 2) + 1, rnd (width_and_height_of_environment -2) + 1 };
        } 
        //Create the obstacles agents
        create obstacle number: number_of_obstacles {
            location &lt;- {rnd (width_and_height_of_environment - 2) + 1, rnd (width_and_height_of_environment -2) + 1 }; 
        }
        //Create the goal that boids will follow
        create  boids_goal; 
    }   
}

//Species boids goal which represents the goal that will be followed by boids agents using the skill moving
species boids_goal skills: [moving] {
    float range  &lt;- 20.0;
    
    //If the mouse is not used, then the goal just wander
    reflex wander {  
        do  wander amplitude: 45 speed: 20;  
    }
    
    aspect default {
        draw circle(10) color: #red ;
        draw circle(40) color: #orange empty: true;
    }
} 
//Species boids which represents the boids agents whom follow the boid goal agents, using the skill moving
species boids skills: [moving] {
    //Speed of the boids agents
    float speed max: maximal_speed &lt;- maximal_speed;
    //Range used to consider the group of the agent
    float range &lt;- minimal_distance * 2;
    point velocity &lt;- {0,0};
        
    //Reflex used when the separation is applied to change the velocity of the boid
    reflex separation when: apply_separation {
        point acc &lt;- {0,0};
        ask (boids overlapping (circle(minimal_distance)))  {
            acc &lt;- acc - ((location) - myself.location);
        }  
        velocity &lt;- velocity + acc;
    }
    
    //Reflex to align the boid with the other boids in the range
    reflex alignment when: apply_alignment {
        list others  &lt;- ((boids overlapping (circle (range)))  - self);
        point acc &lt;- mean (others collect (each.velocity)) - velocity;
        velocity &lt;- velocity + (acc / alignment_factor);
    }
     
    //Reflex to apply the cohesion of the boids group in the range of the agent
    reflex cohesion when: apply_cohesion {
        list others &lt;- ((boids overlapping (circle (range)))  - self);
        point mass_center &lt;- (length(others) &gt; 0) ? mean (others collect (each.location)) : location;

        point acc &lt;- mass_center - location;
        acc &lt;- acc / cohesion_factor; 
        velocity &lt;- velocity + acc;   
    }
    
    //Reflex to avoid the obstacles
    reflex avoid when: apply_avoid { 
        point acc &lt;- {0,0};
        list&lt;obstacle&gt; nearby_obstacles &lt;- (obstacle overlapping (circle (range)) );
        loop obs over: nearby_obstacles {
            acc &lt;- acc - ((location of obs) - my (location));
        }
        velocity &lt;- velocity + acc; 
    }
    
    //action to represent the bounding of the environment considering the velocity of the boid
    action bounding {
        if  !(torus_environment) {
            if  (location.x) &lt; xmin {
                velocity &lt;- velocity + {bounds,0};
            } else if (location.x) &gt; xmax {
                velocity &lt;- velocity - {bounds,0};
            }
            
            if (location.y) &lt; ymin {
                velocity &lt;- velocity + {0,bounds};
            } else if (location.y) &gt; ymax {
                velocity &lt;- velocity - {0,bounds};
            }
            
        }
    }
    //Reflex to follow the goal 
    reflex follow_goal {
        velocity &lt;- velocity + ((first(boids_goal).location - location) / cohesion_factor);
    }
    //Reflex to apply the wind vector on the velocity
    reflex wind when: apply_wind {
        velocity &lt;- velocity + wind_vector;
    }
    
    //Action to move the agent  
    action do_move {  
        if (((velocity.x) as int) = 0) and (((velocity.y) as int) = 0) {
            velocity &lt;- {(rnd(4)) -2, (rnd(4)) - 2};
        }
        point old_location &lt;- copy(location);
        do goto target: location + velocity;
        velocity &lt;- location - old_location;
    }
    
    //Reflex to apply the movement by calling the do_move action
    reflex movement {
        do do_move;
    }
    
    aspect image {
        draw (images at (rnd(2))) size: {50,50} rotate: heading ;      
    }
    aspect circle { 
        draw circle(15)  color: #red;
    }
    
    aspect default { 
        draw circle(20) color: #lightblue empty: true;
    }
} 

//Species obstacle that represents the obstacles avoided by the boids agents using the skill moving
species obstacle skills: [moving] {
    float speed &lt;- 2.0;
    geometry shape &lt;- triangle(15);
    
    //Reflex to move the obstacles if it is available
    reflex move_obstacles when: moving_obstacles {
        //Will make the agent go to a boid with a 50% probability
        if flip(0.5)  
        { 
            do goto target: one_of(boids);
        } 
        else{ 
            do wander amplitude: 360;   
        }
    }
    aspect default {
        draw  triangle(20) color: #black ;
    }

}


experiment boids_gui type: gui {
    parameter 'Number of agents' var: number_of_agents;
    parameter 'Number of obstacles' var: number_of_obstacles;
    parameter 'Maximal speed' var: maximal_speed;
    parameter 'Cohesion Factor' var: cohesion_factor;
    parameter 'Alignment Factor' var: alignment_factor; 
    parameter 'Minimal Distance'  var: minimal_distance; 
    parameter 'Maximal Turn'  var: maximal_turn; 
    parameter 'Width/Height of the Environment' var: width_and_height_of_environment ;  
    parameter 'Toroidal Environment ?'  var: torus_environment ; 
    parameter 'Apply Cohesion ?' var: apply_cohesion ;
    parameter 'Apply Alignment ?' var: apply_alignment ;   
    parameter 'Apply Separation ?' var: apply_separation ;   
    parameter 'Apply Avoidance ?' var: apply_avoid ;   
    parameter 'Apply Wind ?' var: apply_wind ;     
    parameter 'Moving Obstacles ?' var: moving_obstacles  ;    
    parameter 'Direction of the wind' var: wind_vector ;  
    
    //Minimum duration of a step to better see the movements
    float minimum_cycle_duration &lt;- 0.01;

    output {
        display Sky  background: #blue type: opengl { 
            image '../images/sky.jpg' refresh: false;
            species boids aspect: image trace: 10 fading: true ;
            species boids_goal;
            species obstacle;
            //Event to call the action move_goal in global if the mouse move within the experiment
            event mouse_move action: move_goal;
        }

    }
}
</code></pre>
<p>Code of the model :</p>
<pre><code class="hljs">model boids_flock
//Import the boids model
import "Boids.gaml"
global {
    //Size of the boids
    float boids_size &lt;- float(3);
    //Shape of the boids
    geometry boids_shape &lt;- circle(boids_size);
    //Separation between boids
    float boids_separation &lt;- 4 * boids_size;
    //Distance to allow creation of the flock 
    int flock_creation_distance &lt;- int(boids_separation + 1);
    //Minimum number of member among a flock
    int min_group_member &lt;- 3;
    //Frequency of update for the flock
    int update_frequency &lt;- 10;
    //Frequency of merge for the flock
    int merge_frequency &lt;- 10;
    //Allow the creation of flock
    bool create_flocks &lt;- false;
    //Perception range of the boids
    int base_perception_range &lt;- int(xmax / 100) min: 1;
    
    init {
        //Creation of the different agents viewer
        create boids_agents_viewer;
        create flock_agents_viewer;
        create boids_in_flock_viewer;
    }
    //Reflex to create the flocks if it is available
    reflex create_flocks when: create_flocks {
        
        if (length(boids) &gt; 1) {
            //Clustering by distance of the boids to determine the satisfying boids groups
            list&lt;list&lt;boids&gt;&gt; satisfying_boids_groups &lt;- (boids.population simple_clustering_by_distance flock_creation_distance) where ((length(each)) &gt; min_group_member);
            loop one_group over: satisfying_boids_groups {
                
                geometry potential_flock_polygon &lt;- convex_hull(solid(polygon(one_group collect boids(each).location)) + (base_perception_range + 5));
                //If there is no obstacle between the boids of a potential flock, then the flock is created and all the boids become boids in flock
                if (empty(obstacle overlapping potential_flock_polygon)) {
                    create flock {
                        capture one_group as: boids_in_flock;
                    }
                }

            }

        }

    }

}

//Species flock which represent the flock of boids, using the skill moving
species flock skills: [moving] {
    rgb color &lt;- rgb(rnd(255), rnd(255), rnd(255));
    geometry shape &lt;- polygon(((boids_in_flock))) buffer 10;
    //Range of perception of the flock
    float perception_range &lt;- float(base_perception_range + (rnd(5)));
    //Speed of the flock
    float speed update: mean(boids_in_flock collect each.speed);
    //Reflex to disaggregate the flock if there is a obstacle in the flock
    reflex disaggregate {
        geometry buffered_shape &lt;- shape + perception_range;
        if !(empty(obstacle overlapping buffered_shape)) {
            release members as: boids in: world;
            do die;
        }

    }
    //Reflex to capture the boids nearby in the range of perception with an update_frequency
    reflex capture_nearby_boids when: ((cycle mod update_frequency) = 0) {
        geometry buffered_shape &lt;- shape + perception_range;
        list&lt;boids&gt; nearby_boids &lt;- (boids overlapping buffered_shape);
        if (!(empty(nearby_boids))) {
            geometry new_polygon &lt;- convex_hull(solid(shape + polygon(nearby_boids collect (each.location))));
            if (empty(obstacle overlapping new_polygon)) {
                capture nearby_boids as: boids_in_flock;
            }

        }

    }
    //Reflex to merge the intersecting flocks
    reflex merge_nearby_flocks when: ((cycle mod merge_frequency) = 0) 
    {
        loop f over: (flock) {
            if (f != self and (shape intersects f.shape)) {
                geometry new_shape &lt;- convex_hull(polygon(shape.points + f.shape.points));
                if empty(obstacle overlapping new_shape) {
                    list&lt;boids&gt; released_boids;
                    ask f {
                        release members as: boids in: world returns: released_coms;
                        released_boids &lt;- list(released_coms);
                        do die;
                    }

                    if (!empty(released_boids)) {
                        capture released_boids as: boids_in_flock;
                    }

                    shape &lt;- convex_hull(polygon(members collect (boids_in_flock(each).location)));
                }

            }

        }

    }
    //Reflex to make the flock follow the goal
    reflex chase_goal {
        int direction_to_nearest_ball &lt;- (self towards (first(boids_goal)));
        float step_distance &lt;- speed * step;
        float dx &lt;- step_distance * (cos(direction_to_nearest_ball));
        float dy &lt;- step_distance * (sin(direction_to_nearest_ball));
        geometry envelope &lt;- shape.envelope;
        float min_y &lt;- (envelope.points with_min_of (each.y)).y;
        float min_x &lt;- (envelope.points with_min_of (each.x)).x;
        float max_x &lt;- (envelope.points with_max_of (each.x)).x;
        float max_y &lt;- (envelope.points with_max_of (each.y)).y;
        if (((dx + min_x) &lt; xmin) and min_x &gt; xmin) or (((dx + max_x) &gt; xmax) and max_x &lt; xmax) {
            dx &lt;- 0.0;
        }
        if (((dy + min_y) &lt; ymin) and min_y &gt; ymin) or (((dy + max_y) &gt; ymax) and max_y &lt; ymax) {
            dy &lt;- 0.0;
        }
        
        loop com over: boids_in_flock {
            (boids_in_flock(com)).location &lt;- (boids_in_flock(com)).location + { dx, dy };
        }

        shape &lt;- convex_hull(polygon(list(boids_in_flock) collect (each.location)));
    }

    aspect default {
        draw shape color: color;
    }
    //Species boids_in_flock which represents the boids agents captured by the flock
    species boids_in_flock parent: boids {
        float my_age &lt;- 1.0 update: my_age + 0.01;
        reflex separation when: apply_separation {
        }

        reflex alignment when: apply_alignment {
        }

        reflex cohesion when: apply_cohesion {
        }

        reflex avoid when: apply_avoid {
        }

        reflex follow_goal  {
        }

        reflex wind when: apply_wind {
        }

        action do_move {
        }

        reflex movement {
            do do_move;
        }

        aspect default {
            draw circle(my_age) color: ((host as flock).color).darker;
        }

    }

}
//Species flock agents viewer which draw the flock information
species flock_agents_viewer {
    aspect default {
        draw "Flocks: " + (string(length(list(flock)))) at: { width_and_height_of_environment - 810, (width_and_height_of_environment) - 5 } color: #blue size: 80 ;
    }

}
//Species boids agents viewer which draw the boids information
species boids_agents_viewer {
    aspect default {
        draw "Boids: " + (string(length(list(boids)))) at: { width_and_height_of_environment - 810, (width_and_height_of_environment) - 165 } color: #blue size: 80 ;
    }

}

//Species boids_in_flock_viewer which draw the boids in flock information
species boids_in_flock_viewer {
    aspect default {
        draw "Boids in flocks: " + (string(number_of_agents - (length(list(boids))))) at: { width_and_height_of_environment - 810, width_and_height_of_environment - 85 } color:
        #blue size: 80 ;
    }

}


experiment boids_flocks type: gui {
    parameter "Create flock?" var: create_flocks &lt;- true;
    parameter "Number of boids" var: number_of_agents &lt;- 300;
    parameter "Environment size" var: width_and_height_of_environment &lt;- 1600;
    parameter "Moving obstacles?" var: moving_obstacles &lt;- true;
    parameter "Torus environment?" var: torus_environment &lt;- false;
    parameter "Number of obstacles" var: number_of_obstacles &lt;- 5;
    output {
        display default_display {
            species boids_goal;
            species boids aspect: image;
            species obstacle;
            species flock aspect: default transparency: 0.5 {
                species boids_in_flock aspect: default;
            }

            species flock_agents_viewer;
            species boids_agents_viewer;
            species boids_in_flock_viewer;
        }

    }

}
</code></pre>
</span></div></article></div><div class="docs-prevnext"></div></div></div></div><footer class="nav-footer" id="footer"><section class="sitemap"><a href="/BC2019-Gama-Site/" class="nav-home"><img src="/BC2019-Gama-Site/resources/images/general/GamaPlatform.png" alt="GAMA-Platform" width="66" height="58"/></a><div><h5>Docs</h5><a href="/BC2019-Gama-Site/docs/en/doc1.html">Getting Started (or other categories)</a><a href="/BC2019-Gama-Site/docs/en/doc2.html">Guides (or other categories)</a><a href="/BC2019-Gama-Site/docs/en/doc3.html">API Reference (or other categories)</a></div><div><h5>Community</h5><a href="/BC2019-Gama-Site/en/users.html">User Showcase</a><a href="http://stackoverflow.com/questions/tagged/" target="_blank" rel="noreferrer noopener">Stack Overflow</a><a href="https://discordapp.com/">Project Chat</a><a href="https://twitter.com/" target="_blank" rel="noreferrer noopener">Twitter</a></div><div><h5>More</h5><a href="/BC2019-Gama-Site/blog">Blog</a><a href="https://github.com/">GitHub</a></div></section><section class="copyright">Copyright © 2019 GAMA-Platform</section></footer></div><script type="text/javascript" src="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.js"></script><script>
                document.addEventListener('keyup', function(e) {
                  if (e.target !== document.body) {
                    return;
                  }
                  // keyCode for '/' (slash)
                  if (e.keyCode === 191) {
                    const search = document.getElementById('search_input_react');
                    search && search.focus();
                  }
                });
              </script><script>
              var search = docsearch({
                
                apiKey: '0f9f28b9ab9efae89810921a351753b5',
                indexName: 'github',
                inputSelector: '#search_input_react'
              });
            </script></body></html>