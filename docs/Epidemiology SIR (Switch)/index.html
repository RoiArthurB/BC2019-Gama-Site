<!DOCTYPE html><html lang="en"><head><meta charSet="utf-8"/><meta http-equiv="X-UA-Compatible" content="IE=edge"/><title>SIR_switch · GAMA-Platform</title><meta name="viewport" content="width=device-width"/><meta name="generator" content="Docusaurus"/><meta name="description" content="GAMA"/><meta name="docsearch:language" content="en"/><meta property="og:title" content="SIR_switch · GAMA-Platform"/><meta property="og:type" content="website"/><meta property="og:url" content="https://roiarthurb.github.io/BC2019-Gama-Site/"/><meta property="og:description" content="GAMA"/><meta property="og:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><meta name="twitter:card" content="summary"/><meta name="twitter:image" content="https://roiarthurb.github.io/BC2019-Gama-Site//resources/images/general/GamaPlatform.png"/><link rel="shortcut icon" href="/BC2019-Gama-Site//img/gama-logo.png"/><link rel="stylesheet" href="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.css"/><link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.12.0/styles/default.min.css"/><link rel="stylesheet" href="/css/code-block-buttons.css"/><link rel="stylesheet" href="https://use.fontawesome.com/releases/v5.8.2/css/all.css"/><script type="text/javascript" src="https://buttons.github.io/buttons.js" async=""></script><script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/clipboard.js/2.0.0/clipboard.min.js" async=""></script><script type="text/javascript" src="/BC2019-Gama-Site/js/code-block-buttons.js" async=""></script><link rel="stylesheet" href="/BC2019-Gama-Site/css/main.css"/><script src="/BC2019-Gama-Site/js/codetabs.js"></script></head><body class="sideNavVisible"><div class="fixedHeaderContainer"><div class="headerWrapper wrapper"><header><a href="/BC2019-Gama-Site/"><img class="logo" src="/BC2019-Gama-Site//img/gama-logo.png" alt="GAMA-Platform"/><h2 class="headerTitleWithLogo">GAMA-Platform</h2></a><div class="navigationWrapper navigationSlider"><nav class="slidingNav"><ul class="nav-site nav-site-internal"><li class=""><a href="/BC2019-Gama-Site/docs/Home" target="_self">Documentation</a></li><li class=""><a href="/BC2019-Gama-Site/docs/Tutorials" target="_self">Tutorials</a></li><li class=""><a href="/BC2019-Gama-Site/help" target="_self">Download</a></li><li class=""><a href="/BC2019-Gama-Site/help" target="_self">Contribute</a></li><li class=""><a target="_self">Blog</a></li><li class=""><a target="_self"></a></li><li class="navSearchWrapper reactNavSearchWrapper"><input type="text" id="search_input_react" placeholder="Search" title="Search"/></li></ul></nav></div></header></div></div><div class="navPusher"><div class="docMainWrapper wrapper"><div class="container mainContainer"><div class="wrapper"><div class="post"><header class="postHeader"><h1 class="postHeaderTitle">SIR_switch</h1></header><article><div><span><p>_Author : tri and hqnghi _</p>
<p>A model which show how to implement ODE system, IBM model, and to switch from one to another using a threshold. Another interesting point seen in this model is the the minimization of the execution time by reducing the number of agents to compute infections.</p>
<p>Code of the model :</p>
<pre><code class="hljs">model SIR_switch

global {
    <span class="hljs-comment">// Parameters</span>
    int initial_S &lt;- 495 ; <span class="hljs-comment">// The number of susceptible</span>
    int initial_I &lt;- 5   ; <span class="hljs-comment">// The number of infected</span>
    int initial_R &lt;- 0   ; <span class="hljs-comment">// The number of removed </span>

    float beta &lt;- 0.1   ; <span class="hljs-comment">// The parameter Beta </span>
    float delta &lt;- 0.01 ; <span class="hljs-comment">// The parameter Delta    </span>
    
    int switch_threshold &lt;- 120 ; <span class="hljs-comment">// threshold for switching models</span>
    bool local_infection &lt;- true ;
    int neighbors_range &lt;- 2 ;
    bool local_random_walk &lt;- true ; 
    
    
    <span class="hljs-comment">// Global variables</span>
    int grid_size &lt;- 50;
geometry shape &lt;- square(grid_size);
    int number_Hosts &lt;- initial_S + initial_I + initial_R; <span class="hljs-comment">// Total number of individuals</span>
    SIR_model current_model; <span class="hljs-comment">// serves as an interface, it is transparent to user if model is maths or IBM</span>

    float beta_maths;
    int gridSize &lt;- 1; <span class="hljs-comment">//size of the grid</span>
    float neighborhoodSize &lt;- 1.0; <span class="hljs-comment">// average size of the neighborhood (in number of cells) </span>
    float adjust &lt;- 0.721; <span class="hljs-comment">// to adjust math model to ABM when using random walk</span>
    bool computeInfectionFromS &lt;- initial_S &lt; initial_I; <span class="hljs-comment">// if true, use the S list to compute infections. If false, use I list.</span>
    <span class="hljs-comment">// the purpose is to minimize the number of evaluation by using the smallest list.</span>
    
    init {
        create new_scheduler;
        <span class="hljs-comment">/* determine the size of the neighborhood and the average count of hosts neighbors */</span>
        gridSize &lt;- length(sir_grid);
        int nbCells &lt;- 0;
        
        loop cell over: sir_grid {
            nbCells &lt;- nbCells + length(cell.neighbors);
        }

        neighborhoodSize &lt;- nbCells / gridSize + 1; <span class="hljs-comment">// +1 to count itself in the neighborhood;</span>
        beta_maths &lt;- beta * neighborhoodSize * number_Hosts / gridSize * adjust;
        
        <span class="hljs-statement">write</span> <span class="hljs-string">'Switch will happen at population sizes around '</span> +switch_threshold;
        <span class="hljs-statement">write</span> <span class="hljs-string">'Basic Reproduction Number (R0): '</span> + string(beta / delta) + <span class="hljs-string">'\n'</span>;
        
        <span class="hljs-comment">//Creation of the switch_model agent that will manage the switch between the mathematical and the individual based models</span>
        create switch_model {
            threshold_to_IBM &lt;- switch_threshold;
            threshold_to_Maths &lt;- switch_threshold;
        }
        <span class="hljs-comment">//Creation of the model according to the one to begin with</span>
        if (first(switch_model).start_with_IBM) {
        <span class="hljs-comment">//      write 'Starting with IBM model';</span>
            create IBM_model;
            current_model &lt;- first(IBM_model);
        } else {
        <span class="hljs-comment">//      write 'Starting with Maths model';</span>
            create Math_model;
            current_model &lt;- first(Math_model);
        }
        <span class="hljs-comment">//Initialization of the Susceptible, Infected, Resistant and Total Compartiment</span>
        current_model.S &lt;- float(initial_S);
        current_model.I &lt;- float(initial_I);
        current_model.R &lt;- float(initial_R);
        current_model.N &lt;- number_Hosts;
        
        <span class="hljs-comment">//Ask to the model to initialize itself according to the value initialized</span>
        ask current_model {
            do initialize;
        }
        
        <span class="hljs-comment">//Create the SIR maths with ODE to compare</span>
        create my_SIR_maths {
            self.S &lt;- float(myself.initial_S);
            self.I &lt;- float(myself.initial_I);
            self.R &lt;- float(myself.initial_R);
            self.N &lt;- number_Hosts;
            self.beta1 &lt;- beta * neighborhoodSize * (N / gridSize)* adjust;
            self.alpha &lt;- delta;
        }

    }

    reflex infection_computation_method {
    <span class="hljs-comment">/* computing infection from S has a complexity of S*ngb, where ngb is the size of the neighborhood.
     * computing infection from I has a complexity of I*ngb.
     * this reflex determine which method has the lowest cost.
     * */</span>
        computeInfectionFromS &lt;- (Host count (each.is_susceptible)) &lt; (Host count (each.is_infected));
    }

}
<span class="hljs-comment">//Grid which represent the discretized space for the host agents</span>
    grid sir_grid width: grid_size height: grid_size {
        rgb color &lt;- #black;
        list&lt;sir_grid&gt; neighbors &lt;- (self neighbors_at neighbors_range) of_species sir_grid;
    }


<span class="hljs-comment">//Species which allows the execution of only Host, IBM_model, Math_model and switch_model at each cycle</span>
species new_scheduler schedules: (Host + IBM_model + Math_model + switch_model) ;

<span class="hljs-comment">//Species which represent the manager between IBM and Math model</span>
species switch_model schedules: [] {
    int threshold_to_IBM &lt;- 45; <span class="hljs-comment">// threshold under which the model swith to IBM</span>
    int threshold_to_Maths &lt;- 50; <span class="hljs-comment">// threshold under which the model swith to Maths model </span>
    bool start_with_IBM function: { (initial_S &lt; threshold_to_IBM or initial_I &lt; threshold_to_IBM) };

    <span class="hljs-comment">//Switch the model used to IBM when the threshold is higher than the population</span>
    reflex switch_to_IBM when: (current_model.model_type = <span class="hljs-string">'Maths'</span>) {
        if (current_model.S &lt; threshold_to_IBM or current_model.I &lt; threshold_to_IBM) {
            <span class="hljs-statement">write</span> <span class="hljs-string">'Switch to IBM model at cycle '</span> + string(cycle);
            create IBM_model {
                self.S &lt;- current_model.S;
                self.I &lt;- current_model.I;
                self.R &lt;- current_model.R;
                self.N &lt;- current_model.N;
                do initialize;
            }

            ask current_model {
                do remove_model;
            }

            current_model &lt;- first(IBM_model);
        }

    }
    <span class="hljs-comment">//Switch the model used to Maths when the threshold is lower than the population</span>
    reflex switch_to_Maths when: (current_model.model_type = <span class="hljs-string">'IBM'</span>) {
        if (current_model.S &gt; threshold_to_Maths and current_model.I &gt; threshold_to_Maths) {
            <span class="hljs-statement">write</span> <span class="hljs-string">'Switch to Maths model at cycle '</span> + cycle;
            create Math_model {
                self.S &lt;- current_model.S;
                self.I &lt;- current_model.I;
                self.R &lt;- current_model.R;
                self.N &lt;- current_model.N;
                do initialize;
            }

            ask current_model {
                do remove_model;
            }

            current_model &lt;- first(Math_model);
        }

    }

}
<span class="hljs-comment">//Species which represent the SIR model used by the IBM and the Math models </span>
species SIR_model schedules: [] {
    float S;
    float I;
    float R;
    int N;
    string model_type &lt;- <span class="hljs-string">'none'</span>;
    
    action remove_model {
        do die;
    }

    action initialize ;

}

<span class="hljs-comment">//Species IBM Model which represent the Individual based model, derivated from SIR_model</span>
species IBM_model schedules: [] parent: SIR_model {
    string model_type &lt;- <span class="hljs-string">'IBM'</span>;
    
    <span class="hljs-comment">//Action to initialize the Model with SIR compartiments</span>
    action initialize {
        
        <span class="hljs-statement">write</span> <span class="hljs-string">'Initializing IBM model with S='</span> + round(S) + <span class="hljs-string">', I='</span> + round(I) + <span class="hljs-string">', R='</span> + round(R) + <span class="hljs-string">'\n'</span>;
        <span class="hljs-comment">//Creation of the host agents</span>
        create Host number: round(S) {
            is_susceptible &lt;- true;
            is_infected &lt;- false;
            is_immune &lt;- false;
            color &lt;- #green;
        }

        create Host number: round(I) {
            is_susceptible &lt;- false;
            is_infected &lt;- true;
            is_immune &lt;- false;
            color &lt;- #red;
        }

        create Host number: round(R) {
            is_susceptible &lt;- false;
            is_infected &lt;- false;
            is_immune &lt;- true;
            color &lt;- #yellow;
        }
        do count;
    }

    reflex count {
        do count;
    }
    <span class="hljs-comment">//Action to update the different compartiments</span>
    action count {
        S &lt;- float(Host count (each.is_susceptible));
        I &lt;- float(Host count (each.is_infected));
        R &lt;- float(Host count (each.is_immune));
    }
    <span class="hljs-comment">//Action to remove the model and kill all the agents it contains</span>
    action remove_model {
        ask Host {
            do die;
        }

        do die;
    }

}

<span class="hljs-comment">//Species Math Model which represent the mathematical Ordinary Differential Equations model, derivated from SIR_model</span>
species Math_model schedules: [] parent: SIR_model {
    string model_type &lt;- <span class="hljs-string">'Maths'</span>;
    float t;
    
    action initialize {
        <span class="hljs-statement">write</span> <span class="hljs-string">'Initializing Maths model with S='</span> + S + <span class="hljs-string">', I='</span> + I + <span class="hljs-string">', R='</span> + R + <span class="hljs-string">'\n'</span>;
    }

    equation SIR {
        diff(S, t) = (-beta_maths * S * I / N);
        diff(I, t) = (beta_maths * S * I / N) - (delta * I);
        diff(R, t) = (delta * I);
    }

    reflex solving {solve SIR method: <span class="hljs-string">"rk4"</span> step: 0.01 ;}
}
<span class="hljs-comment">//Species host used by the Individual Based Model which move from one cell to another</span>
species Host schedules: [] skills: [moving] {
    bool is_susceptible &lt;- true;
    bool is_infected &lt;- false;
    bool is_immune &lt;- false;
    rgb color &lt;- #green;
    sir_grid myPlace;
    
    <span class="hljs-comment">/* next function computes the number of neighbors of the agent */</span>
    int ngb_number function: {
        length(((self) neighbors_at (2)) of_species Host) - 1 <span class="hljs-comment">// -1 is because the agent counts itself</span>
    };
    
    init {
        myPlace &lt;- one_of(sir_grid as list);
        location &lt;- myPlace.location;
    }

    <span class="hljs-comment">//Reflex to move the agents among the cells</span>
    reflex basic_move {
        if (!local_random_walk) {
        <span class="hljs-comment">/* random walk among neighbors */</span>
            myPlace &lt;- one_of(myPlace.neighbors);
            location &lt;- myPlace.location;
        } else {
        <span class="hljs-comment">/* move agent to a random place anywhere in the grid */</span>
            myPlace &lt;- any(sir_grid);
            location &lt;- myPlace.location;
        }

    }
    <span class="hljs-comment">//Reflex to make the agent infected when the infection is computed from S for a better execution time</span>
    reflex become_infected when: (is_susceptible and computeInfectionFromS) {
        if (flip(1 - (1 - beta) ^ (((self) neighbors_at (2)) of_species Host) count (each.is_infected))) {
            is_susceptible &lt;- false;
            is_infected &lt;- true;
            is_immune &lt;- false;
            color &lt;- #red;
        }

    }
    <span class="hljs-comment">//Reflex to make the agent infect others when the infection is not computed from S for a better execution time</span>
    reflex infecte_others when: (is_infected and not (computeInfectionFromS)) {
        loop hst over: ((self) neighbors_at (2)) {
            if (hst.is_susceptible) {
                if (flip(beta)) {
                    hst.is_susceptible &lt;- false;
                    hst.is_infected &lt;- true;
                    hst.is_immune &lt;- false;
                    hst.color &lt;- #red;
                }
            }
        }
    }
    <span class="hljs-comment">//Reflex to make the agent resistant</span>
    reflex become_immune when: (is_infected and flip(delta)) {
        is_susceptible &lt;- false;
        is_infected &lt;- false;
        is_immune &lt;- true;
        color &lt;- #yellow;
    }

    aspect basic {
        draw circle(1) color: color;
    }

}
<span class="hljs-comment">//Species which represent the SIR mathematical model </span>
species my_SIR_maths {
    float t;
    float I &lt;- float(iInit);
    float S &lt;- N - I;
    float R &lt;- 0.0;
            
    float alpha &lt;- 0.01 min: 0.0 max: 1.0;
    float beta1 &lt;- 0.1 min: 0.0 max: 1000.0;
    int N &lt;- 500 min: 1 max: 3000;
    int iInit &lt;- 1;

    equation SIR {
        diff(S, t) = (-beta1 * S * I / N);
        diff(I, t) = (beta1 * S * I / N) - (alpha * I);
        diff(R, t) = (alpha * I);
    }
    
    reflex solving {solve SIR method:<span class="hljs-string">"rk4"</span> step:0.01;}

}



experiment mysimulation type: gui {
    parameter <span class="hljs-string">'Number of Susceptible'</span> type: int var: initial_S &lt;- 495 category: <span class="hljs-string">"Initial population"</span>; 
    parameter <span class="hljs-string">'Number of Infected'</span>    type: int var: initial_I &lt;- 5   category: <span class="hljs-string">"Initial population"</span>;
    parameter <span class="hljs-string">'Number of Removed'</span>     type: int var: initial_R &lt;- 0   category: <span class="hljs-string">"Initial population"</span>;

    parameter <span class="hljs-string">'Beta (S-&gt;I)'</span>  type: float var: beta &lt;- 1.0   category: <span class="hljs-string">"Parameters"</span>;
    parameter <span class="hljs-string">'Delta (I-&gt;R)'</span> type: float var: delta &lt;- 0.01 category: <span class="hljs-string">"Parameters"</span>; 
    
    parameter <span class="hljs-string">'Is the infection is computed locally?'</span> type: bool var: local_infection &lt;- true category: <span class="hljs-string">"Infection"</span>;
    parameter <span class="hljs-string">'Size of the neighbors'</span> type: int var: neighbors_range &lt;- 2 min:1 max: 5 category: <span class="hljs-string">"Infection"</span>;

    parameter <span class="hljs-string">'Local Random Walk'</span> type: bool var: local_random_walk &lt;- true category: <span class="hljs-string">"Agents"</span>; 
    
    parameter <span class="hljs-string">'Switch models at'</span> type: int var: switch_threshold &lt;- 120 category: <span class="hljs-string">"Model"</span>;
    
    output {
        display <span class="hljs-string">'sir display'</span> {
            grid sir_grid lines: #black;
            species Host aspect: basic;
        }
    
        display <span class="hljs-string">'Switch model'</span> {
            chart <span class="hljs-string">'Susceptible'</span> type: series background: #lightgray style: exploded {
                data <span class="hljs-string">'susceptible'</span> value: current_model.S color: #green;
                data <span class="hljs-string">'infected'</span> value: current_model.I color: #red;
                data <span class="hljs-string">'immune'</span> value: current_model.R color: #yellow;
            }

        }

        display SI_maths  {
            chart <span class="hljs-string">"SI"</span> type: series background: #white {
                data <span class="hljs-string">'S'</span> value: first((my_SIR_maths)).S color: #green;
                data <span class="hljs-string">'I'</span> value: first((my_SIR_maths)).I color: #red;
                data <span class="hljs-string">'R'</span> value: first((my_SIR_maths)).R color: #yellow;
            }

        }

    }

}
</code></pre>
</span></div></article></div><div class="docs-prevnext"></div></div></div></div><footer class="nav-footer" id="footer"><section class="sitemap"><a href="/BC2019-Gama-Site/" class="nav-home"><img src="/BC2019-Gama-Site//img/gama-logo.png" alt="GAMA-Platform" width="66" height="58"/></a><div><h5>Networks</h5><a href="https://www.youtube.com/channel/UCWJ1kWGDDI-9u2f2uD0gcaQ" target="_blank"><i class="fab fa-youtube"></i> Youtube</a><a href="https://www.linkedin.com/company/gama-platform" target="_blank"><i class="fab fa-linkedin"></i> LinkedIn</a><a href="https://twitter.com/gamaplatform" target="_blank"><i class="fab fa-twitter"></i> Twitter</a><a href="https://www.facebook.com/GamaPlatform/" target="_blank"><i class="fab fa-facebook"></i> Facebook</a></div><div><h5>Mailing list</h5><a href="https://groups.google.com/forum/#!forum/gama-platform" target="_blank"><i class="fas fa-envelope"></i> For Users<br/>gama-platform@googlegroups.com</a><a href="https://groups.google.com/forum/#!forum/gama-dev" target="_blank"><i class="fas fa-envelope"></i> For Developers<br/>gama-dev@googlegroups.com</a></div><div><h5>Ecosystem</h5><a href="https://github.com/gama-platform/gama"><i class="fab fa-github"></i> GitHub Gama</a><a href="https://github.com/gama-platform/gama.experimental"><i class="fas fa-puzzle-piece"></i> Gama Experimental Plugins</a><a href="https://github.com/gama-platform/gama.models.experimental"><i class="fas fa-scroll"></i> Gama Experimental Models</a></div></section><section class="copyright">Copyright © 2019 GAMA-Platform</section></footer></div><script type="text/javascript" src="https://cdn.jsdelivr.net/docsearch.js/1/docsearch.min.js"></script><script>
                document.addEventListener('keyup', function(e) {
                  if (e.target !== document.body) {
                    return;
                  }
                  // keyCode for '/' (slash)
                  if (e.keyCode === 191) {
                    const search = document.getElementById('search_input_react');
                    search && search.focus();
                  }
                });
              </script><script>
              var search = docsearch({
                
                apiKey: '0f9f28b9ab9efae89810921a351753b5',
                indexName: 'github',
                inputSelector: '#search_input_react'
              });
            </script></body></html>